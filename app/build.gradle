apply plugin: 'com.android.application'

static def gitVersionCode() {
  def cmd = 'git rev-list HEAD --first-parent --count'
  cmd.execute().text.trim().toInteger()
}

static def gitVersionTag() {
  def cmd = 'git describe --tags'
  def version = cmd.execute().text.trim()
  def pattern = "-(\\d+)-g"
  def matcher = version =~ pattern

  if (matcher) {
    version = version.substring(0, matcher.start())
  }
  return version
}

android {
  compileSdkVersion 25
  buildToolsVersion "25.0.2"
  defaultConfig {
    applicationId "cn.cattlefish.bodybuilding"
    minSdkVersion 16
    targetSdkVersion 25
    versionCode 1
    versionName "1.0"
    ndk {
      //设置支持的SO库架构
      abiFilters 'armeabi', 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
    }
  }
  applicationVariants.all { variant ->
    variant.mergedFlavor.versionCode = gitVersionCode()
    variant.mergedFlavor.versionName = gitVersionTag()
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
}

dependencies {
//  compile 'com.github.lypeer:GoogleClock:1.0.0'
  compile fileTree(dir: 'libs', include: ['*.jar'])
  compile 'com.android.support:appcompat-v7:25.3.1'
  compile 'com.tencent.bugly:crashreport_upgrade:latest.release'
  compile 'com.tencent.bugly:nativecrashreport:latest.release'
}
